Program -> kwProgram ident ; Declarations Block .

Declarations -> 
Declarations -> Declaration Declarations

Declaration -> Function
Declaration -> Procedure
Declaration -> Constants
Declaration -> Variables

Function -> FunctionHeader FunctionOrProcedureRest

FunctionHeader -> kwFunction ident ( NamesWithTypes ) : ident ;

Procedure -> ProcedureHeader FunctionOrProcedureRest

ProcedureHeader -> kwProcedure ident ( NamesWithTypes ) ;

FunctionOrProcedureRest -> MaybeVariables Block ;
FunctionOrProcedureRest -> kwForward ;

NamesWithTypes ->
NamesWithTypes -> NamesWithType MoreNamesWithTypes

MoreNamesWithTypesSemicolon -> 
MoreNamesWithTypesSemicolon -> NamesWithType MoreNamesWithTypesSemicolon ;

MoreNamesWithTypes ->
MoreNamesWithTypes -> ; NamesWithType MoreNamesWithTypes

NamesWithType -> Names : Type

Names -> ident MoreNames

MoreNames ->
MoreNames -> , ident MoreNames

Type -> ident
Type -> kwArray [ litInt .. litInt ] kwOf ident

Variables -> kwVar NamesWithType ; MoreNamesWithTypes2

MoreNamesWithTypes2 -> NamesWithType ; MoreNamesWithTypes2
MoreNamesWithTypes2 -> 

MaybeVariables ->
MaybeVariables -> Variables

Constants -> kwConst Constant ; MoreConstants

MoreConstants -> Constant ; MoreConstants
MoreConstants -> 

Constant -> ident = litInt

Block -> kwBegin Statements kwEnd

Statements ->
Statements -> Statement ; Statements

Statement -> If
Statement -> While
Statement -> For
Statement -> Block
Statement -> AssignOrCall

AssignOrCall -> ident AssignOrCallRest

AssignOrCallRest -> ( Params )
AssignOrCallRest -> := Expression

Params ->
Params -> Expression MoreParams

MoreParams ->
MoreParams -> , Expression MoreParams

If -> kwIf Expression kwThen Statement MaybeElse

MaybeElse ->
MaybeElse -> Else

Else -> kwElse Statement

While -> kwWhile Expression kwDo Statement

For -> kwFor ident := Range kwDo Statement

Range -> litInt RangeRest 

RangeRest -> kwTo litInt
RangeRest -> kwDownto litInt

Expression -> E0

E0 -> E1 MoreE0
MoreE0 -> or E0
MoreE0 ->

E1 -> E2 MoreE1
MoreE1 -> and E1
MoreE1 ->

E2 -> not E2
E2 -> E3

E3 -> E4 E3Rest
E3R -> = E4
E3R -> <> E4
E3R -> < E4
E3R -> > E4
E3R -> <= E4
E3R -> >= E4
E3R ->

E4 -> E5 MoreE4
MoreE4 -> + E4
MoreE4 -> - E4
MoreE4 ->

E5 -> E6 MoreE5
MoreE5 -> * E5
MoreE5 -> div E5
MoreE5 -> mod E5
MoreE5 ->

E6 -> + E6
E6 -> - E6
E6 -> E7

E7 -> ( E0 )
E7 -> ReadOrCall
E7 -> litInt

ReadOrCall -> ident ReadOrCallRest

ReadOrCallRest ->
ReadOrCallRest -> ( Params )
