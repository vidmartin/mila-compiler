Program -> kwProgram ident ; Declarations Block .

Declarations -> 
Declarations -> Declaration Declarations

Declaration -> Function
Declaration -> Procedure
Declaration -> Constants
Declaration -> Variables

Function -> FunctionHeader FunctionOrProcedureRest

FunctionHeader -> kwFunction ident ( NamesWithTypes ) : ident ;

Procedure -> ProcedureHeader FunctionOrProcedureRest

ProcedureHeader -> kwProcedure ident ( NamesWithTypes ) ;

FunctionOrProcedureRest -> MaybeVariables Block ;
FunctionOrProcedureRest -> kwForward ;

NamesWithTypes ->
NamesWithTypes -> NamesWithType MoreNamesWithTypes

MoreNamesWithTypes ->
MoreNamesWithTypes -> ; NamesWithType MoreNamesWithTypes

NamesWithType -> Names : Type

Names -> ident MoreNames

MoreNames ->
MoreNames -> , ident MoreNames

Type -> ident
Type -> kwArray [ litInt .. litInt ] kwOf Type

Variables -> kwVar NamesWithType ; MoreNamesWithTypes2

MoreNamesWithTypes2 -> NamesWithType ; MoreNamesWithTypes2
MoreNamesWithTypes2 -> 

MaybeVariables ->
MaybeVariables -> Variables

Constants -> kwConst Constant ; MoreConstants

MoreConstants -> Constant ; MoreConstants
MoreConstants -> 

Constant -> ident = litInt

Block -> kwBegin Statements kwEnd

Statements -> 
Statements -> Statement MoreStatements

MoreStatements -> 
MoreStatements -> ; Statements

Statement -> If
Statement -> While
Statement -> For
Statement -> Block
Statement -> ExpressionOrAssignment

Params ->
Params -> E0 MoreParams

MoreParams ->
MoreParams -> , E0 MoreParams

If -> kwIf E0 kwThen Statement MaybeElse

MaybeElse ->
MaybeElse -> Else

Else -> kwElse Statement

While -> kwWhile E0 kwDo Statement

For -> kwFor ident := Range kwDo Statement

Range -> E0 RangeRest 

RangeRest -> kwTo E0
RangeRest -> kwDownto E0

ExpressionOrAssignment -> E0 ExpressionOrAssignmentRest

ExpressionOrAssignmentRest -> := E0
ExpressionOrAssignmentRest -> 

E0 -> E1 MoreE0
MoreE0 -> or E0
MoreE0 ->

E1 -> E2 MoreE1
MoreE1 -> and E1
MoreE1 ->

E2 -> not E2
E2 -> E3

E3 -> E4 E3R
E3R -> = E4
E3R -> <> E4
E3R -> < E4
E3R -> > E4
E3R -> <= E4
E3R -> >= E4
E3R ->

E4 -> E5 MoreE4
MoreE4 -> + E4
MoreE4 -> - E4
MoreE4 ->

E5 -> E6 MoreE5
MoreE5 -> * E5
MoreE5 -> div E5
MoreE5 -> mod E5
MoreE5 ->

E6 -> + E6
E6 -> - E6
E6 -> E7

E7 -> ( E0 ) MoreE7
E7 -> ReadOrCall MoreE7
E7 -> litInt MoreE7
E7 -> litStr MoreE7

MoreE7 -> [ E0 ] MoreE7
MoreE7 -> 

ReadOrCall -> ident ReadOrCallRest

ReadOrCallRest ->
ReadOrCallRest -> ( Params )
